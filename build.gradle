// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    apply(from: "$rootDir/dependencies.gradle")
    dependencies {
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$versions.navVersion"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$versions.detekt"
    }
}

plugins {
    id 'com.android.application' version '8.0.1' apply false
    id 'com.android.library' version '8.0.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.7.21' apply false
    id "com.google.devtools.ksp" version "1.7.21-1.0.8"
}

allprojects {
    apply from: "$rootDir/ktlint.gradle"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects { pr ->

    if (project.name == "app") {
        apply(plugin: "com.android.application")
        android {
            defaultConfig {
                applicationId application.id
            }
        }
    } else {
        apply(plugin: "com.android.library")
        apply(plugin: "kotlin-android")
    }
    android {
        defaultConfig {
            compileSdk application.maxApi
            minSdk application.minApi
            targetSdk application.targetSdk
            versionCode application.versionCode
            versionName application.versionName
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions.jvmTarget = JavaVersion.VERSION_11.toString()
    }
}
